# $Id$
# Maintainer: Axel Moinet (BuLLeKeUp) <bullekeup@gmail.com>
#
# Based on Xerpi's vita-portlibs
# See https://github.com/xerpi/vita_portlibs

pkgname=arm-vita-lib-sqlite
_pkgname=sqlite
pkgver=3100000
pkgrel=1
pkgdesc="PSVita SQLite build"
arch=('i686' 'x86_64')
url="http://sqlite.org"
license=('custom')
groups=('vita-toolchain-portlibs')
depends=('arm-vita-eabi-gcc' 'arm-vita-eabi-libs' 'arm-vita-eabi-tools')
makedepends=('git' 'arm-vita-eabi-tools' 'arm-vita-eabi-gcc' 'arm-vita-eabi-libs')
source=("http://sqlite.org/2016/${_pkgname}-autoconf-${pkgver}.tar.gz")
md5sums=('274364e6ca5c1104d42912f11e61ed26')
options=(!strip)

CTARGET="arm-vita-eabi"
DIR_NATIVE=/opt/vita-toolchain/${CHOST}
DIR_VITA=/opt/vita-toolchain/${CTARGET}
PATH="${DIR_VITA}/usr/bin:${PATH}"

export PKG_CONFIG_PATH=${DIR_VITA}/usr/lib/pkgconfig
export CFLAGS="-std=c99 -ftree-vectorize -O3 -ffat-lto-objects -flto -mword-relocations -fomit-frame-pointer -DSQLITE_OS_OTHER=1"
export CPPFLAGS="-I${DIR_VITA}/usr/include -I${DIR_VITA}/usr/arm-vita-eabi/include"
export LDFLAGS="-L${DIR_VITA}/usr/lib -L${DIR_VITA}/usr/arm-vita-eabi/lib"

build() {

  cd ${srcdir}/${_pkgname}-autoconf-${pkgver}
  ./configure --disable-shared --disable-threadsafe --disable-dynamic-extensions --prefix=${DIR_VITA}/usr --host=${CTARGET}
  make libsqlite3.la
  
}

package() {

  cd ${srcdir}/${_pkgname}-autoconf-${pkgver}
  make DESTDIR=${pkgdir} install-libLTLIBRARIES install-data
  ( [ -e ${pkgdir}/${DIR_VITA}/usr/share/info/dir ] && rm ${pkgdir}/${DIR_VITA}/usr/share/info/dir ) || echo "No info dir to delete"

}

# vim:set ts=2 sw=2 et:
