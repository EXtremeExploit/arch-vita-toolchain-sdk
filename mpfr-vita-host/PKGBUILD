# $Id$
# Maintainer: Axel Moinet (BuLLeKeUp) <bullekeup@gmail.com>

pkgname=mpfr-vita-host
_pkgname=mpfr
_pkgver=3.1.4
_patchlevel=p3
pkgver=${_pkgver}.${_patchlevel}
pkgrel=1
pkgdesc="Multiple-precision floating-point library - PSVita toolchain build"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
makedepends=('gmp-vita-host>=5.0')
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        mpfr-${_pkgver}-${_patchlevel}.patch)
md5sums=('064b2c18185038e404a401b830d59be8'
         'SKIP'
         '8e643974afcc897082ba2ede1b5cc589')
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')
groups=('vita-toolchain-hostlibs')

#unset CFLAGS
#unset CXXFLAGS
#CHOST="${arch}-pc-linux-gnu"
CTARGET="arm-vita-eabi"
DIR_NATIVE=/opt/vita-toolchain/${CHOST}
DIR_VITA=/opt/vita-toolchain/${CTARGET}

prepare() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  patch -p1 -i ${srcdir}/${_pkgname}-${_pkgver}-${_patchlevel}.patch
}

build() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"

  ./configure --build=${CHOST} \
    --host=${CHOST} \
    --target=${CTARGET} \
    --prefix=${DIR_NATIVE}/usr \
    --disable-shared \
    --disable-nls \
    --with-gmp=${DIR_NATIVE}/usr
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
  ( [ -e ${pkgdir}/${DIR_NATIVE}/usr/share/info/dir ] && rm ${pkgdir}/${DIR_NATIVE}/usr/share/info/dir ) || echo "No info dir to delete"
}
